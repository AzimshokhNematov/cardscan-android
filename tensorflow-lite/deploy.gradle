apply plugin: 'maven-publish'
apply plugin: 'signing'

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''

ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

ext {

    libraryDescription = 'This library provides a custom implementation of TFLite for Bouncer products.'

    siteUrl = 'https://getbouncer.com'

    scmConnection = 'scm:git:github.com/getbouncer/cardscan-android.git'
    scmDeveloperConnection = 'scm:git:https://github.com/getbouncer/cardscan-android.git'
    scmUrl = 'https://github.com/getbouncer/cardscan-android'

    licenseName = 'bouncer-free-1'
    licenseUrl = 'https://github.com/getbouncer/cardscan-android/blob/master/LICENSE'

    developerId = 'getbouncer'
    developerName = 'Bouncer Technologies'
    developerEmail = 'bouncer-support@stripe.com'

    publishGroupId = 'com.getbouncer'
    publishArtifactId = 'tensorflow-lite'
    publishVersion = version
}

group = publishGroupId
version = publishVersion

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')

    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')

    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId publishGroupId
            artifactId publishArtifactId
            version publishVersion
            artifact "$buildDir/../tensorflow-lite-all-models.aar"

            pom {
                name = publishArtifactId
                description = libraryDescription
                url = siteUrl
                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }
                developers {
                    developer {
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }
                scm {
                    connection = scmConnection
                    developerConnection = scmDeveloperConnection
                    url = scmUrl
                }
            }
        }
    }

    // The repository to publish to, Sonatype/MavenCentral
    repositories {
        maven {
            // This is an arbitrary name, you may also use "mavencentral" or
            // any other name that's descriptive for you
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}
